plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs'
    id 'kotlinx-serialization'
}

apply from: 'ktlint.gradle'

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.mbrainz.sample"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding = true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            // TODO: for demo purposes, obviously a proper keystore should be used and passed as a secret
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'

    // navigation
    def navigation_component_version = '2.5.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_component_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_component_version"

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    // coroutines
    def coroutines_version = '1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutines_version}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutines_version}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:${coroutines_version}"

    // koin
    def koin_version = "3.2.2"
    def koin_android_version = "3.2.3"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_android_version"

    // logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Koin Test features
    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"

    def androidx_espresso_version = "3.5.1"
    def barista_version = "4.2.0"

    // androidx test
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "androidx.test:rules:1.5.0"
    testImplementation "androidx.test:runner:1.5.2"
    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    debugImplementation "androidx.fragment:fragment-testing:1.5.5"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation "io.mockk:mockk:1.11.0"
    testImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_version"
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation("com.adevinta.android:barista:$barista_version") {
        exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
    testImplementation 'app.cash.turbine:turbine:0.12.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    debugImplementation "androidx.test:monitor:1.6.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_version"
    androidTestImplementation("com.adevinta.android:barista:$barista_version") {
        exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
    androidTestImplementation ('org.awaitility:awaitility:4.2.0') {
        exclude group: 'org.hamcrest' // Only if you already use Kotlin in your project
    }
}